NB. After 40755, what is the next triangle number that is also pentagonal and hexagonal?
NB. 
NB. Triangle, pentagonal, and hexagonal numbers are generated by the following formulae:
NB. Triangle 	  	Tn=n(n+1)/2 	  	1, 3, 6, 10, 15, ...
NB. Pentagonal 	  	Pn=n(3n-1)/2 	  	1, 5, 12, 22, 35, ...
NB. Hexagonal 	  	Hn=n(2n-1) 	  	1, 6, 15, 28, 45, ...
NB. 
NB. It can be verified that T285 = P165 = H143 = 40755.
NB. 
NB. Find the next triangle number that is also pentagonal and hexagonal.






NB. olegyk:
P=: (-: * <:@(3&*)) 1+i.100000
H=: (* <:@+:) 1+i.100000
15": (P e.H)#P
1 40755 1533776805





NB. JavaManissa:
NB. Idea here is generate 100,000 pentagonal and hexagonal (sic). Combine the two arrays, return duplicates.
(#~(-.@:~:))(((*-&1@:+:)143x+i.1e5),((%&2)@:(*<:@:*&3)167x +i.1e5))





NB. crooks_dwayne:
T =: monad : '(y.*(y.+1))%2' NB. triangle numbers
P =: monad : '(y.*(_1 + 3*y.))%2' NB. pentagonal numbers
H =: monad : '(y.*(_1+2*y.))' NB. hexagonal numbers

NB. x. and y. are sets;
NB. result is intersection,
NB. in the order given in x.
and =: e. # [

n =: 1000000
a =: T i.n
b =: P i.n
c =: H i.n

NB. numbers less than n that are
NB. triangular, pentagonal and hexagonal
d =: 15": a and b and c




NB. Eratosthenes
tn=.-:*>:
pn=.-:*[:<:3&*
hn=.*[:<:+:
x:(tn"0(e.#[)pn"0(e.#[)hn"0)]>:i.1e5


NB. vim:ft=j
